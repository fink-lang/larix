{add_operator, add_separator, add_non_separating} = import '@fink/prattler/symbols'

{infix, terminator} = import '../symbols'

{add_named_block, terminated_block} = import '../block'
{end_of_block_indent} = import '../block/indentation'

{call_expr} = import './expr'


call = fn op, end_symbol:
  {
    ...infix:: op

    led: fn: fn ctx, callee:
      call_expr:: ctx, callee, op, fn ctx: terminated_block:: ctx, end_symbol
  }


add_call_with_parens = fn ctx:
  pipe ctx:
    add_operator:: call:: '(', ')'
    add_separator:: terminator:: ','
    add_separator:: terminator:: ')'


add_call_without_parens = fn ctx:
  pipe ctx:
    add_operator:: call:: '::', end_of_block_indent


add_pipe = fn ctx:
   pipe ctx:
    add_named_block:: 'pipe'


add_call_operators = fn ctx:
  pipe ctx:
    add_call_with_parens
    add_call_without_parens
    add_pipe

