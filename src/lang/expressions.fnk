{symbol} = import '@fink/prattler/symbols.fnk'
{curr_loc} = import '@fink/prattler/parser.fnk'

{next_is_new_expr} = import './block/indentation.fnk'
{single_expression} = import './block/expr.fnk'



infix_led = fn type, op, lbp: fn ctx, left:
  {loc: {start}} = left
  [right, next_ctx] = single_expression ctx, lbp
  {loc: {end}} = right

  [{type, op, left, right, loc: {start, end}}, next_ctx]


left_binding = fn lbp: fn ctx:
  match ctx:
    next_is_new_expr ?:
      0
    else:
      lbp


non_binding = fn: fn: 0


infix = fn op, type:
  dict:
    ...symbol op

    lbp: left_binding

    led: fn lbp: infix_led type, op, lbp + 1


infix_right = fn op, type:
  dict:
    ...symbol op

    lbp: left_binding

    led: fn lbp: infix_led type, op, lbp - 1


prefix = fn op, type:
  dict:
    ...symbol op

    lbp: non_binding

    nud: fn lbp: fn ctx:
      {start} = curr_loc ctx
      [right, next_ctx] = single_expression ctx, lbp
      {end} = right.loc

      [{type, op, right, loc: {start, end}}, next_ctx]


literal = fn op, type:
  dict:
    ...symbol op

    lbp: non_binding

    nud: fn: fn ctx:
      loc = curr_loc ctx
      # value = curr_value ctx
      [{type, value: op, loc}, ctx]


terminator = fn op:
  dict:
    ...symbol op

    lbp: left_binding

