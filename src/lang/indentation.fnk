{curr_loc, next_loc} = import '@fink/prattler'

# TODO: should this be part of ../block
# using push_block/pop_block


indentation = fn {indent}:
  [first] = indent
  first


pop_indentation = fn ctx, ind:
  match true:
    ind < indentation:: ctx:
      [, ...indent] = ctx.indent
      pop_indentation:: {...ctx, indent}, ind
    else:
      ctx


push_indentation = fn ctx, ind:
  {...ctx, indent: [ind, ...ctx.indent]}


next_is_unindented = fn ctx:
  # TODO: test that next token is aligned with any of the indentations
  {start: {column}} = next_loc:: ctx
  column < indentation:: ctx


curr_next_adjecent = fn ctx:
  curr = curr_loc:: ctx
  next = next_loc:: ctx

  curr.end.line == next.start.line && curr.end.column == next.start.column


init_indentation = fn ctx:
  {...ctx, indent: [0]}
