{
  next_is_end, next_is, next_loc, curr_loc, collect_text, advance, expression
} = import '@fink/prattler'

{next_is_unindented} = import '../indentation'
{symbol} = import '../symbols'


collect_single_comment = fn ctx, op:
  [text, next_ctx] = collect_text:: ctx,  '\n'
  [{op, ...text}, next_ctx]


collect_comments = fn ctx, op:
  [comment, next_ctx] = collect_single_comment:: ctx, op

  match next_ctx:
    # TODO: check not unindented?
    next_is:: ?, '#':
      [comments, final_ctx] = collect_comments::
        advance:: next_ctx
        op
      [[comment, ...comments], final_ctx]
    else:
      [[comment], next_ctx]


comment = fn op:
  {
    ...symbol(op),

    nud: fn: fn ctx:
      {start} = curr_loc:: ctx

      [items, expr_ctx] = collect_comments:: ctx, op

      {end} = curr_loc:: expr_ctx
      loc = {start, end}

      match expr_ctx:
        next_is_end:: ?:
          [{type: 'comment', op, exprs: items, loc}, expr_ctx]

        next_is_unindented:: ?:
          [{type: 'comment', op, exprs: items, loc}, expr_ctx]

        else:
          # TODO: should lbp be really 0
          [{comments, ...expr}, next_ctx] = expression:: expr_ctx, 0
          [{...expr, comments: {...comments, leading: items}}, next_ctx]
  }
